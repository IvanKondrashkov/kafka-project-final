plugins {
    id 'java'
    id 'org.springframework.boot' apply true
}

group 'ru.practicum'
version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.spring.io/milestone'
    }
}

ext {
    spark = '3.5.6'
    hadoop = '3.3.4'
    snappy = '1.1.10.5'
    elasticsearch = '8.5.3'
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation project(':common-dto')
    implementation 'ch.qos.logback:logback-classic'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.glassfish.jersey.containers', module: 'jersey-container-servlet'
        exclude group: 'org.glassfish.jersey.core', module: 'jersey-server'
        exclude group: 'org.glassfish.jersey.inject', module: 'jersey-hk2'
    }
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'

    implementation "org.apache.spark:spark-core_2.12:$spark"
    implementation "org.apache.spark:spark-sql_2.12:$spark"
    implementation "org.apache.spark:spark-yarn_2.12:$spark"
    implementation "org.apache.spark:spark-streaming_2.12:$spark"
    implementation "org.apache.spark:spark-sql-kafka-0-10_2.12:$spark"
    implementation "org.apache.spark:spark-streaming-kafka-0-10_2.12:$spark"

    implementation "org.apache.hadoop:hadoop-client:$hadoop"
    implementation "org.elasticsearch:elasticsearch-spark-30_2.12:$elasticsearch"
    implementation "org.elasticsearch.client:elasticsearch-rest-client:$elasticsearch"

    implementation("org.xerial.snappy:snappy-java:$snappy") {
        exclude group: "org.osgi", module: "org.osgi.core"
    }
}

configurations.configureEach {
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'

    exclude group: 'javax.servlet', module: 'servlet-api'
    exclude group: 'org.eclipse.jetty.orbit', module: 'javax.servlet'

    exclude group: 'org.elasticsearch', module: 'elasticsearch'
    exclude group: 'org.elasticsearch.client', module: 'transport'
}

test {
    useJUnitPlatform()
}